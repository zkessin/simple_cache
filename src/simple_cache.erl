-module(simple_cache).

-export([insert/2, lookup/1, delete/1, lookup_or_insert/2, search/1, delete_pattern/1]).

lookup_or_insert(Key, ValueFun) ->
    case lookup(Key) of 
	{ok, Value} ->
	    Value;
	_ ->
	    Value = ValueFun(),
	    insert(Key, Value),
	    Value
    end.

insert(Key, Value) ->
    case sc_store:lookup(Key) of
        {ok, Pid} ->
            sc_event:replace(Key, Value),
            sc_element:replace(Pid, Value);
        {error, _} ->
            {ok, Pid} = sc_element:create(Value),
            sc_store:insert(Key, Pid),
            sc_event:create(Key, Value)
    end.

lookup(Key) ->
    sc_event:lookup(Key),
    try
        {ok, Pid} = sc_store:lookup(Key),
        {ok, Value} = sc_element:fetch(Pid),
        {ok, Value}
    catch
        _Class:_Exception ->
            {error, not_found}
    end.


delete(Key) ->
    sc_event:delete(Key),
    case sc_store:lookup(Key) of
        {ok, Pid} ->
            sc_element:delete(Pid);
        {error, _Reason} ->
            ok
    end.

search(Pattern) ->
    sc_store:search(Pattern).

delete_pattern(Pattern) ->
    Pids = search(Pattern),
    lists:foreach(fun(Pid) ->
			  sc_element:delete(Pid)
		  end, Pids),
    length(Pids).
